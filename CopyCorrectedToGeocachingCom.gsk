#*******************************************
# MacDescription = Update Corrected Coordinates on Geocaching.Com
# MacFileName = CopyCorrectedToGeocachingCom.gsk
# MacAuthor = bjoff
# MacVersion= 1.04
# MacUrl = 
#*******************************************

# v1.01 - 2017-07-27 Initial commit, after a bit rewrite
# v1.02 - 2017-07-30 Bugfix: Checks for wrong custom-field at startup.
# v1.03 - 2017-08-09 Now we also sends caches that has not CorrDate (ie corrected
#                    before CorrectedDateToCustomField was installed
# v1.04 - 2017-08-11 Update a corrected coordinate is not allowed, so now we
#                    delete it first if it already exists corrected coordinates 
#                    on geocaching.com
#


# Oppdaterer korrigerte koordinater på geocaching.com.
#
#
# Krav for å benytte denne makroen:
#  * Installer makro CorrectedDateToCustomField : http://gsak.net/board/index.php?showtopic=26381
#  * Legg inn makro fra steget over i 'Corrected Coordinates' dialogen under 'Macro to run after OK'
#  * Alle cacher som skal oppdateres på geocaching.com må ha en gyldig dato i feltet 'CorrDate'
#  
# Når dette er på plass vil makroen holde styr på hvilke cacher som er endret lokalt og som ikke er 
# send til geocaching.com.
# Dersom korrigerte koordinater slettes vil makroen fjerne koordinatene fra gc.com 
# 

VERCHECK Version=8.6.0.0 (Please update GSAK to version 8.6 or later to run this macro)

# Check if we have the macro "CorrectedDateToCustomField" installed and setup.
$_sql="PRAGMA table_info(Custom)"
$CustomCol=Sqlite("sql",$_sql)
If At("CorrDate",$CustomCol)=0
    MsgOk msg=Avslutter. Du må først installere og kjøre makroen CorrectedDateToCustomField
    Return
EndIf


# Some housekeeping, should only run first time we use the macro.
$_sql="PRAGMA table_info(Custom)"
$CustomCol=Sqlite("sql",$_sql)
If At("CorrUpdate",$CustomCol)=0
    $Status=CustomConfig("Add","CorrUpdate,Date,Local,,,,,")
EndIf


# Chose caches which should be updated
MFilter where=(CorrDate > '01-01-2000' and CorrDate >= CorrUpdate) or (CorrDate = '' and CorrUpdate = '' and HasCorrected)
If $_FilterCount = 0
  MsgOk msg=Avslutter. Ingen cacher trenger oppdatering
  Return
EndIf


$CurrNum = 1
$TotalNum = $_FilterCount
$Added   = 0
$Removed = 0


Goto Position=TOP
While Not ($_EOL)

  # If corrected, we try to update
  If $d_HasCorrected
    Gosub name=SendCorrected
    $APIres = val(sqlite("sql","select data FROM GCapi where key = '/SaveUserWaypointResponse/Status/StatusCode'"))

    # Already exists, we first need to delete on gc.com, then try again
    If $APIres = 155

      Gosub name=DeleteCorrected
      $APIres = val(sqlite("sql","select data FROM GCapi where key = '/StatusResponse/StatusCode'"))
      If $APIres <> 0
        MsgOk msg="There was an error with the API request (SaveUserWaypoint) Status is $APIres"
      EndIf

      Gosub name=SendCorrected
      $APIres = val(sqlite("sql","select data FROM GCapi where key = '/SaveUserWaypointResponse/Status/StatusCode'"))
      If $APIres <> 0
        MsgOk msg="There was an error with the API request (SaveUserWaypoint) Status is $APIres"
      EndIf

    EndIf
    $Added = $Added + 1


  # Not corrected anymore, delete from geocaching.com
  Else
    Gosub name=DeleteCorrected
    $APIres = val(sqlite("sql","select data FROM GCapi where key = '/SaveUserWaypointResponse/Status/StatusCode'"))
    If $APIres <> 0
      MsgOk msg="There was an error with the API request (SaveUserWaypoint) Status is $APIres"
    EndIf

    $Removed = $Removed + 1
  EndIf
  
  $statusMessage = "$CurrNum / $TotalNum (Added: $Added / Removed: $Removed)"
  ShowStatus msg=$statusMessage Width=350
  
  # Update CustomField CurrUpdate
  $dummy = CustomPut("CorrUpdate",DateToSql($_Today))

  $CurrNum = $CurrNum + 1
  Goto Position=Next
EndWhile

MsgOk msg="Finished: $Added added - $Removed removed"

BeginSub name=SendCorrected
  $XmlAPI=$APITemplate
  $XmlAPI=Replace("~GCCODE~",    $d_Code,      $XmlAPI)
  $XmlAPI=Replace("~LATITUDE~",  $d_Latitude,  $XmlAPI)
  $XmlAPI=Replace("~LONGITUDE~", $d_Longitude, $XmlAPI)
  $retVal = GcApi("SaveUserWaypoint",$XmlAPI)
  If $_GcApiError
    MsgOk msg="There was an error with the API request (SaveUserWaypoint)"
  EndIf
#
#MsgOk msg="Finished with SaveUserWaypoint"
#
EndSub

BeginSub name=DeleteCorrected
  $WayPoints = GcApi("GetUserWaypoints?cacheCode=$d_Code")
  $WpToDelete = val(sqlite("sql","select data FROM GCapi where key = '/GetUserWaypointsResponse/UserWaypoints/a:UserWaypoint/a:ID' "))
  If $WpToDelete > 1
    $retVal = GcApi("DeleteUserWaypoint?waypointId=$WpToDelete")
    If $_GcApiError
      MsgOk msg="There was an error with the API request (DeleteUserWaypoint)"
    EndIf
  EndIf
#
#MsgOk msg="Finished with GetUserWaypoints"
#
EndSub

<data> VarName=$APITemplate
<SaveUserWaypointRequest xmlns="http://www.geocaching.com/Geocaching.Live/data">
  <AccessToken>{ACCESSTOKEN}</AccessToken>
  <CacheCode>~GCCODE~</CacheCode>
  <Latitude>~LATITUDE~</Latitude>
  <Longitude>~LONGITUDE~</Longitude>
  <ID>0</ID>
  <IsCorrectedCoordinate>true</IsCorrectedCoordinate>
  <IsUserCompleted>true</IsUserCompleted>
</SaveUserWaypointRequest>
<enddata>
